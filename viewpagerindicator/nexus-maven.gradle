apply from: 'gradle.properties'

apply plugin: 'maven'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: MAVEN_SNAPSHOT_URL) {
                authentication(userName: MAVEN_USER_NAME, password: MAVEN_PASSWORD)
            }
            repository(url: MAVEN_URL) {
                authentication(userName: MAVEN_USER_NAME, password: MAVEN_PASSWORD)
            }
            pom.project {
                name LIBRARY_NAME
                version VERSION_NAME
                artifactId ARTIFACT_ID
                groupId GROUP_ID
                packaging MAVEN_TYPE
                description LIBRARY_DESCRIPTION
            }
        }
    }
}
